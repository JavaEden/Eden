group 'com.caseyjbrooks'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

configurations {
    orchidDocs
}

sourceSets {
    orchidDocs
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    orchidDocs('com.github.JavaEden:Orchid:ffda31f7d8') {
        transitive = true
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.github.cjbrooks12:Clog:v1.0.0'
}

// Jacoco Code Coverage tasks
//------------------------------------------------------------------------------
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

// Documentation generation tasks using custom-styled Orchid doclet
//------------------------------------------------------------------------------
javadoc {
    title = ""
    source = sourceSets.main.allJava

    options.docletpath.addAll(configurations.orchidDocs)
    options.doclet = 'com.eden.orchid.Orchid'

    options.addStringOption("colors", "#c00 #000000")
    options.addStringOption("resourcesDir", sourceSets.orchidDocs.resources.srcDirs[0].toString())
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
