group 'com.caseyjbrooks'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

configurations {
    doclava
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    doclava 'com.github.JavaEden:doclava:v0.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.github.cjbrooks12:Clog:v1.0.0'
}

// Jacoco Code Coverage tasks
//------------------------------------------------------------------------------
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Doclava
//------------------------------------------------------------------------------

javadoc {
    source = sourceSets.main.allJava

    title = ""

    options.docletpath.addAll(configurations.doclava)
    options.doclet = 'com.google.doclava.Doclava'
}

// Documentation generation tasks
//------------------------------------------------------------------------------
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


